
CREATE FUNCTION [Reporting.Investment].[ufn_GetAUMs]
(
	@PositionDate DATE = NULL
)
RETURNS  @Output TABLE (
	[FUND_SHORT_NAME] VARCHAR(256) NULL,
	[VALUATION_DATE] DATETIME NULL,
	[DESCRIPTION] VARCHAR(255) NULL,
	[VALUE] DECIMAL(18,2) NULL,
	[IsEstimate] BIT DEFAULT 1,
	[LastUpdatedDate] DATETIME NULL
	)
AS
BEGIN
	DECLARE @Date DATE

	IF @PositionDate IS NULL
	BEGIN
		SET @PositionDate = CONVERT(DATE,GETDATE())
	END

	SELECT @Date = MAX(POSITION_DATE) FROM [dbo].[T_MASTER_FND_MARKET_VALUE]
	WHERE CONVERT(DATE,POSITION_DATE) <= @PositionDate

  INSERT INTO @OUTPUT ([FUND_SHORT_NAME], [VALUATION_DATE], [DESCRIPTION], [VALUE], [IsEstimate], [LastUpdatedDate])
	SELECT LEFT([FUND_SHORT_NAME],6), POSITION_DATE, 'NT AUM', [TOTAL_ACCRUED_MARKET_VALUE_BASE], 0, [CADIS_SYSTEM_LASTMODIFIED]
	FROM [dbo].[T_MASTER_FND_MARKET_VALUE]
	WHERE CONVERT(DATE, [POSITION_DATE]) = @Date

;WITH CTE_FX AS 
(
	SELECT SPOT_RATE, FROM_ISO_CURRENCY_CODE FROM 
	 [dbo].[T_MASTER_FXRATE] WHERE [Date] = (
	SELECT MAX(Date) FROM [dbo].[T_MASTER_FXRATE]
	WHERE Date <= @Date
	) AND TO_ISO_CURRENCY_CODE = 'GBP'
)
INSERT INTO @OUTPUT ([FUND_SHORT_NAME], [VALUATION_DATE], [DESCRIPTION], [VALUE], [IsEstimate], [LastUpdatedDate])
	SELECT 
	'WIMOFF'
	,[ValuationDate]
	, 'NT AUM'
      ,SUM([NetAssets] * COALESCE(fx.SPOT_RATE,1.0))
	  , 0
      ,MAX([CADIS_SYSTEM_LASTMODIFIED]) 
  FROM [Operation].[WEIFFeederFundPrice]
  LEFT JOIN CTE_FX fx ON fx.FROM_ISO_CURRENCY_CODE = Currency
  WHERE ValuationDate = @Date
  GROUP BY [ValuationDate]

  -- To support a bug with Tableau.  Must provide a default value of 0 for WIMOFF
  IF NOT EXISTS (SELECT 1 FROM @OUTPUT WHERE [FUND_SHORT_NAME] = 'WIMOFF')
  BEGIN
	INSERT INTO @OUTPUT ([FUND_SHORT_NAME], [VALUATION_DATE], [DESCRIPTION], [VALUE], [IsEstimate], [LastUpdatedDate])
	SELECT 
	'WIMOFF'
	,@Date
	, 'NT AUM'
      ,0
	  , 0
      ,GETDATE() 
  END
	RETURN
END

