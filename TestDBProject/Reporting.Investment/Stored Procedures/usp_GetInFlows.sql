
CREATE PROCEDURE [Reporting.Investment].[usp_GetInFlows]
	@ReportDate DATE = NULL
AS
	DECLARE @SJPDate DATE
	SET NOCOUNT ON
IF @ReportDate IS NULL
BEGIN
	SET @ReportDate = CONVERT(DATE, GETDATE())
END

-- SJP is at T. AUM and flows are at T+1
SET @SJPDate = (CASE WHEN DATENAME(DW, @ReportDate) = 'MONDAY' THEN DATEADD(dd, -3, @ReportDate) ELSE DATEADD(dd, -1, @ReportDate) END)

DECLARE @Output TABLE (
	[FUND_SHORT_NAME] VARCHAR(256) NULL,
	[IN_FLOW_DATE] DATETIME NULL,
	[FUND_FLOW_TYPE] VARCHAR(15) NULL,
	[FLOW_TYPE] VARCHAR(15) NULL,
	[VALUE] DECIMAL(18,2) NULL,
	[LastUpdatedDate] DATETIME NULL
)

INSERT INTO @Output (
		[FUND_SHORT_NAME]
      ,[IN_FLOW_DATE]
	  ,[FUND_FLOW_TYPE]
      ,[FLOW_TYPE]
	  ,[VALUE] 
	  ,[LastUpdatedDate]
)
SELECT * FROM
(
	SELECT
		'OMNIS1' AS FUND_SHORT_NAME
		,[VALUATION_POINT_DATE] AS TRANSACTION_DATE
		,'GROSS' AS FUND_FLOW_TYPE
		,(CASE WHEN SUM([DECISION_VALUE]) < 0 THEN 'REDEMPTION' ELSE 'SUBSCRIPTION' END) AS FLOW_TYPE
		,SUM([DECISION_VALUE]) AS MARKET_VALUE
		,MAX([CADIS_SYSTEM_UPDATED]) AS [CADIS_SYSTEM_UPDATED]
	FROM [dbo].[T_MASTER_FND_SHARE_CLASS_FLOW] AS Confirmed
	GROUP BY [VALUATION_POINT_DATE]
) data
	WHERE CONVERT(DATE,[TRANSACTION_DATE]) = @ReportDate

-- If we do not have the confirmed numbers for OMNIS1 then use the estimates
IF NOT EXISTS (SELECT TOP 1 1 FROM @Output)
BEGIN
	INSERT INTO @Output (
		[FUND_SHORT_NAME]
      ,[IN_FLOW_DATE]
	  ,[FUND_FLOW_TYPE]
      ,[FLOW_TYPE]
	  ,[VALUE]
	  ,[LastUpdatedDate]
	  )
	SELECT 
		'OMNIS1' AS FUND_SHORT_NAME
      , [TRANSACTION_DATE] AS [TRANSACTION_DATE]
	  ,[FUND_FLOW_TYPE]
      , [FLOW_TYPE] AS [FLOW_TYPE]
	  ,(CASE WHEN [FLOW_TYPE] = 'REDEMPTION' THEN -1 ELSE 1 END) * [MARKET_VALUE] AS [MARKET_VALUE]
	  ,[CADIS_SYSTEM_UPDATED]
  FROM 
	[dbo].[T_MASTER_GROSS_FUND_FLOW]
	WHERE CONVERT(DATE,[TRANSACTION_DATE]) = @ReportDate
	AND FUND_SHORT_NAME = 'OMNIS1'
END

;WITH CTE_FX AS (
	SELECT fx.[FXRATE_ID], [SPOT_RATE]
	FROM [dbo].[T_MASTER_FXRATE] fx
	INNER JOIN (
	SELECT [FXRATE_ID]
      ,MAX([DATE]) AS [DATE]
	  FROM [dbo].[T_MASTER_FXRATE]
	  WHERE [DATE] <= @ReportDate
	  GROUP BY [FXRATE_ID]
  ) data ON data.[FXRATE_ID] = fx.[FXRATE_ID] AND data.[DATE] = fx.[DATE]
), CTE_FEEDER AS
(
	SELECT SUM(VALUE) AS VALUE, 'GBP' AS CCY,
	(CASE WHEN [TYPE] IN ('Sale','Class Switch Sale') THEN 'SUBSCRIPTION' ELSE 'REDEMPTION' END) AS FLOW_TYPE
	, MAX([CADIS_SYSTEM_UPDATED]) AS [CADIS_SYSTEM_UPDATED]
	FROM [dbo].[T_MASTER_DEALS_IN_PROGRESS]
	WHERE CONVERT(DATE, VALUATION_POINT) = @ReportDate
	AND MAIN_OWNER_NAME = 'Northern Trust Nominees (Ireland) Ltd'
	GROUP BY (CASE WHEN [TYPE] IN ('Sale','Class Switch Sale') THEN 'SUBSCRIPTION' ELSE 'REDEMPTION' END)
)
INSERT INTO @Output (
		[FUND_SHORT_NAME]
      ,[IN_FLOW_DATE]
	  ,[FUND_FLOW_TYPE]
      ,[FLOW_TYPE]
	  ,[VALUE]
	  ,[LastUpdatedDate]
	  )
SELECT LTRIM(RTRIM(REPLACE(REPLACE([FUND_SHORT_NAME], CHAR(13), ''), CHAR(10), ''))) AS [FUND_FLOW_NAME]
      ,CONVERT(DATE,[TRANSACTION_DATE]) AS [IN_FLOW_DATE]
      ,[FUND_FLOW_TYPE]
      ,[FLOW_TYPE]
      ,[MARKET_VALUE] * (CASE WHEN CURRENCY = 'GBP' THEN 1 ELSE [SPOT_RATE] END) * (CASE WHEN FLOW_TYPE = 'REDEMPTION' THEN -1 ELSE 1 END)
	   AS [VALUE]
	  ,[CADIS_SYSTEM_UPDATED]
  FROM [dbo].[T_MASTER_FUND_FLOW] ff
  LEFT JOIN CTE_FX fx ON CURRENCY + 'GBP' = fx.FXRATE_ID
  WHERE CONVERT(DATE,[TRANSACTION_DATE]) = @ReportDate
  AND LTRIM(RTRIM(REPLACE(REPLACE([FUND_SHORT_NAME], CHAR(13), ''), CHAR(10), ''))) NOT IN('SJPDST', 'SJPXUK', 'SJPNUK', 'OMNIS1')
  AND FUND_FLOW_TYPE = 'GROSS'
UNION ALL
  SELECT [FUND_SHORT_NAME] AS [FUND_FLOW_NAME]
      ,CONVERT(DATE,[TRANSACTION_DATE]) AS [IN_FLOW_DATE]
      ,[FUND_FLOW_TYPE]
      ,[FLOW_TYPE]
      ,[MARKET_VALUE] * (CASE WHEN CURRENCY = 'GBP' THEN 1 ELSE [SPOT_RATE] END) * (CASE WHEN FLOW_TYPE = 'REDEMPTION' THEN -1 ELSE 1 END) AS [VALUE]
	  ,[CADIS_SYSTEM_UPDATED]
  FROM [dbo].[T_MASTER_FUND_FLOW]
   LEFT JOIN CTE_FX fx ON CURRENCY + 'GBP' = fx.FXRATE_ID
  WHERE CONVERT(DATE,[TRANSACTION_DATE]) = @SJPDate
  AND LTRIM(RTRIM(REPLACE(REPLACE([FUND_SHORT_NAME], CHAR(13), ''), CHAR(10), ''))) IN('SJPDST', 'SJPXUK', 'SJPNUK')
   AND FUND_FLOW_TYPE = 'GROSS'
UNION ALL
   SELECT [FUND_SHORT_NAME] AS [FUND_FLOW_NAME]
      ,CONVERT(DATE,[TRANSACTION_DATE]) AS [IN_FLOW_DATE]
      ,[FUND_FLOW_TYPE]
      ,[FLOW_TYPE]
      ,[MARKET_VALUE] * (CASE WHEN CURRENCY = 'GBP' THEN 1 ELSE [SPOT_RATE] END) * (CASE WHEN FLOW_TYPE = 'REDEMPTION' THEN -1 ELSE 1 END) 
	  AS [VALUE]
	  ,[CADIS_SYSTEM_UPDATED]
  FROM [dbo].[T_MASTER_FUND_FLOW]
  LEFT JOIN CTE_FX fx ON CURRENCY + 'GBP' = fx.FXRATE_ID
  WHERE CONVERT(DATE,[TRANSACTION_DATE]) = @ReportDate
  AND FUND_FLOW_TYPE = 'NET'
UNION ALL
   SELECT 'WIMOFF' AS [FUND_FLOW_NAME]
      ,@ReportDate AS [IN_FLOW_DATE]
      ,'GROSS'
      ,FLOW_TYPE
      ,[VALUE] * (CASE WHEN CCY = 'GBP' THEN 1 ELSE [SPOT_RATE] END) * (CASE WHEN FLOW_TYPE = 'REDEMPTION' THEN -1 ELSE 1 END) 
	  AS [VALUE]
	  ,[CADIS_SYSTEM_UPDATED]
  FROM CTE_FEEDER
  LEFT JOIN CTE_FX fx ON CCY + 'GBP' = fx.FXRATE_ID

  -- To support a bug with Tableau.  Must provide a default value of 0 for WIMOFF
  IF NOT EXISTS (SELECT 1 FROM @OUTPUT WHERE [FUND_SHORT_NAME] = 'WIMOFF' AND [FLOW_TYPE] = 'SUBSCRIPTION')
  BEGIN
	INSERT INTO @OUTPUT ([FUND_SHORT_NAME]
      ,[IN_FLOW_DATE]
	  ,[FUND_FLOW_TYPE]
      ,[FLOW_TYPE]
	  ,[VALUE]
	  ,[LastUpdatedDate])
	SELECT 
	'WIMOFF'
	,@ReportDate
	, 'GROSS'
      ,'SUBSCRIPTION'
	  , 0
      ,GETDATE() 
  END

  IF NOT EXISTS (SELECT 1 FROM @OUTPUT WHERE [FUND_SHORT_NAME] = 'WIMOFF' AND [FLOW_TYPE] = 'REDEMPTION')
  BEGIN
	INSERT INTO @OUTPUT ([FUND_SHORT_NAME]
      ,[IN_FLOW_DATE]
	  ,[FUND_FLOW_TYPE]
      ,[FLOW_TYPE]
	  ,[VALUE]
	  ,[LastUpdatedDate])
	SELECT 
	'WIMOFF'
	,@ReportDate
	, 'GROSS'
      ,'REDEMPTION'
	  , 0
      ,GETDATE() 
  END 
  
  SELECT 
		fnd.SHORT_NAME AS FUND_SHORT_NAME
		,fnd.LONG_NAME AS FUND_LONG_NAME
		,o.FLOW_TYPE
		,o.FUND_FLOW_TYPE
		,o.IN_FLOW_DATE
		,o.VALUE
		,o.LastUpdatedDate
   FROM [dbo].[T_MASTER_FND] fnd
   LEFT JOIN @Output o ON o.FUND_SHORT_NAME = fnd.SHORT_NAME

